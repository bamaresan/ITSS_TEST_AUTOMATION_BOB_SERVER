*-----------------------------------------------------------------------------
* <Rating>-41</Rating>
*-----------------------------------------------------------------------------
    SUBROUTINE TAUT.SOURCE.STAT.REPORT
    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INCLUDE TAUT.BP I_F.TAUT.PARAMETER

    GOSUB OPEN.FILES
    GOSUB READ.PARAMETER
    GOSUB PROCESS

    RETURN

OPEN.FILES:
*-----------
    SEPARATOR = ','
    FINAL.REPORT = ''
    FN.USER = 'F.USER'
    F.USER = ''
    CALL OPF(FN.USER,F.USER)

    FN.TAUT.PARAM = 'F.TAUT.PARAMETER'
    F.TAUT.PARAM = ''
    CALL OPF(FN.TAUT.PARAM,F.TAUT.PARAM)
    STAT.REP.HEADER   = "TRANSACTION.DATE" :SEPARATOR: "TRANSACTION.TIME" :SEPARATOR: "APPLICATION"  :SEPARATOR: "REFERENCE NO."  :SEPARATOR: "OPERATION "  :SEPARATOR: "VERSION NAME"  :SEPARATOR: "USER NAME"  :SEPARATOR: "BRANCH INFO"

    RETURN

READ.PARAMETER:
*---------------
    R.TAUT.PARAM = ''
    TAUT.PARAM.READ.ERR = ''
    CALL F.READ(FN.TAUT.PARAM,'SYSTEM',R.TAUT.PARAM,F.TAUT.PARAM,TAUT.PARAM.READ.ERR)

    FN.STORE.SRC.OFS = R.TAUT.PARAM<TAUT.STORE.TXNS.OFS>
    F.STORE.SRC.OFS = ''
    CALL OPF(FN.STORE.SRC.OFS,F.STORE.SRC.OFS)

    FN.SOURCE.STAT.REPORT = R.TAUT.PARAM<TAUT.SOURCE.REPORTS>
    F.SOURCE.STAT.REPORT = ''
    CALL OPF(FN.SOURCE.STAT.REPORT,F.SOURCE.STAT.REPORT)

    RETURN

PROCESS:
*--------
    STAT.REPORT = ''
    SEL.CMD = "SELECT ":FN.STORE.SRC.OFS
    CALL EB.READLIST(SEL.CMD,TXN.LIST,'','','')
    LOOP
        REMOVE TXN.QUEUE.REF FROM TXN.LIST SETTING TXN.POS
    WHILE TXN.QUEUE.REF:TXN.POS
        GOSUB INIT.VAR
        TXN.DATE.INFO  = FIELD(TXN.QUEUE.REF,'_',1)
        TXN.DATE =  FIELD(TXN.DATE.INFO,'.',1)
        TIME.INFO = FIELD(TXN.DATE.INFO,'.',2)
        SECONDS.INFO = FIELD(TXN.DATE.INFO,'.',3)
        TXN.TIME =  TIME.INFO[1,2] : ":" :TIME.INFO[3,2]: ":" :TIME.INFO[5,2]: ' ' : SECONDS.INFO

        TXN.APPLICATION = FIELD(TXN.QUEUE.REF,'_',2)
        TXN.APPLICATION.REFERENCE =  FIELD(TXN.QUEUE.REF,'_',3)
        TXN.FUNCTION = FIELD(TXN.QUEUE.REF,'_',4)
        TXN.APPLICATION.VERSION =  FIELD(TXN.QUEUE.REF,'_',5)
        TXN.USER.NAME = FIELD(TXN.QUEUE.REF,'_',6)
        TXN.COMPANY.ID = FIELD(TXN.QUEUE.REF,'_',7)

        STAT.REPORT<-1>  = TXN.DATE  : SEPARATOR: TXN.TIME :SEPARATOR: TXN.APPLICATION :SEPARATOR: TXN.APPLICATION.REFERENCE :SEPARATOR: TXN.FUNCTION  :SEPARATOR:  TXN.APPLICATION:',':TXN.APPLICATION.VERSION :SEPARATOR: TXN.USER.NAME :SEPARATOR: TXN.COMPANY.ID
    REPEAT

    FINAL.REPORT = STAT.REP.HEADER :FM: STAT.REPORT
    STAT.REPORT.ID = "Statistic Report from Source Environment":'_':TODAY

    CALL F.WRITE(FN.SOURCE.STAT.REPORT,STAT.REPORT.ID,FINAL.REPORT)

    RETURN

INIT.VAR:
*--------
    TIME.INFO = ''
    TXN.DATE.INFO  =  ''
    SECONDS.INFO = ''
    TXN.DATE =  ''
    TXN.TIME = ''
    TXN.APPLICATION = ''
    TXN.APPLICATION.REFERENCE =  ''
    TXN.FUNCTION = ''
    TXN.APPLICATION.VERSION =  ''
    TXN.USER.NAME = ''
    TXN.COMPANY.ID = ''

    RETURN

END






