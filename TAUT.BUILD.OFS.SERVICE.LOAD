*-----------------------------------------------------------------------------
* <Rating>-52</Rating>
*-----------------------------------------------------------------------------
*Modification History:-
*20160923  - Amaresan - No.Input , No.Change field will be identified based on the Input Version.
*                       So, T() array has been captured in Version.control.
*20160927 - Amaresan  - No.change filed check is wrong .
*-----------------------------------------------------------------------------

    SUBROUTINE TAUT.BUILD.OFS.SERVICE.LOAD

    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_F.SPF
    $INSERT I_F.JOURNAL
    $INSERT I_F.COMPANY
    $INSERT I_F.FILE.CONTROL
    $INSERT I_F.STANDARD.SELECTION
    $INSERT I_F.VERSION
    $INSERT I_EB.TRANS.COMMON
    $INSERT I_IO.EQUATE
    $INSERT I_F.ACCOUNT
    $INSERT I_F.FOREX
    $INSERT I_F.LD.LOANS.AND.DEPOSITS
    $INSERT I_F.LD.SCHEDULE.DEFINE
    $INSERT I_F.MM.MONEY.MARKET
    $INSERT I_F.FD.FID.ORDER
    $INSERT I_F.FD.FIDUCIARY
    $INSERT I_F.SEC.TRADE
    $INSERT I_GTS.COMMON
    $INSERT I_F.LOCKING
    $INSERT I_F.EB.PHANTOM
    $INSERT I_F.USER
    $INCLUDE TAUT.BP I_F.TAUT.PARAMETER
    $INCLUDE TAUT.BP I_F.TAUT.APPL.DETAILS
    $INCLUDE TAUT.BP I_TAUT.BUILD.OFS.SERVICE.COMMON


    GOSUB OPEN.FILES
    GOSUB READ.PARAMETER
    GOSUB OPEN.TAUT.LOG.FILE

    RETURN

OPEN.FILES:
*-----------
    FN.USER = 'F.USER'
    F.USER = ''
    CALL OPF(FN.USER,F.USER)

    FN.VERSION = 'F.VERSION'
    F.VERSION = ''
    CALL OPF(FN.VERSION,F.VERSION)

    FN.FC = 'F.FILE.CONTROL'
    F.FC = ''
    CALL OPF(FN.FC,F.FC)

    FN.COMPANY = 'F.COMPANY'
    F.COMPANY = ''
    CALL OPF(FN.COMPANY,F.COMPANY)

    FN.TAUT.PARAM = 'F.TAUT.PARAMETER'
    F.TAUT.PARAM = ''
    CALL OPF(FN.TAUT.PARAM,F.TAUT.PARAM)

    FN.TAUT.APP.DET = 'F.TAUT.APPL.DETAILS'
    F.TAUT.APP.DET = ''
    CALL OPF(FN.TAUT.APP.DET,F.TAUT.APP.DET)

    FN.ACCOUNT = 'F.ACCOUNT'
    F.ACCOUNT = ''
    CALL OPF(FN.ACCOUNT, F.ACCOUNT)

    RETURN

READ.PARAMETER:
*---------------
    R.TAUT.PARAM = ''
    TAUT.PARAM.READ.ERR = ''
    CALL F.READ(FN.TAUT.PARAM,'SYSTEM',R.TAUT.PARAM,F.TAUT.PARAM,TAUT.PARAM.READ.ERR)

    FN.STORE.SRC.OFS = R.TAUT.PARAM<TAUT.STORE.TXNS.OFS>
    F.STORE.SRC.OFS = ''
    CALL OPF(FN.STORE.SRC.OFS,F.STORE.SRC.OFS)

    FN.STORE.SRC.JNL = "F.TAUT.STORE.JNL"
    F.STORE.SRC.JNL = ''
    CALL OPF(FN.STORE.SRC.JNL,F.STORE.SRC.JNL)

    FN.PROCESSED.SRC.JNL = R.TAUT.PARAM<TAUT.PROCESSED.JNL>
    F.PROCESSED.SRC.JNL =  ''
    CALL OPF(FN.PROCESSED.SRC.JNL,F.PROCESSED.SRC.JNL)

    FN.STORE.TXNS= "F.TAUT.STORE.TXN"
    F.STORE.TXNS= ''
    CALL OPF(FN.STORE.TXNS,F.STORE.TXNS)

    FN.MOVE.STORE.TXNS = R.TAUT.PARAM<TAUT.STORE.TXNS>
    F.MOVE.STORE.TXNS  = ''
    CALL OPF(FN.MOVE.STORE.TXNS,F.MOVE.STORE.TXNS)

    DATA.SYNC = R.TAUT.PARAM<TAUT.DATA.SYNC>

    R.TAUT.APP.DET = ''
    TAUT.APP.DET.READ.ERR = ''
    CALL F.READ(FN.TAUT.APP.DET,'SYSTEM',R.TAUT.APP.DET,F.TAUT.APP.DET,TAUT.APP.DET.READ.ERR)

    GOSUB GET.PARAMETER.DETAILS

    RETURN

GET.PARAMETER.DETAILS:
**********************

    TRANS.TYPE    = R.TAUT.PARAM<TAUT.TRANS.TYPE>
    EXCL.FUNCTION = R.TAUT.PARAM<TAUT.EXCL.FUNCTION>
    EXCL.VERSION  = R.TAUT.PARAM<TAUT.EXCL.VERSION>
    EXCL.APPL     = R.TAUT.PARAM<TAUT.EXCL.APPL>
    EXCL.USER     = R.TAUT.PARAM<TAUT.EXCL.USER>
    EXCL.COMPANY  = R.TAUT.PARAM<TAUT.EXCL.COMPANY>
    TGT.T24.RELEASE = R.TAUT.PARAM<TAUT.TGT.T24.REL>
    SAMPLE.CNTR   = R.TAUT.PARAM<TAUT.SAMPLE.CNTR>
    PRD.ONLINE.JNL = R.TAUT.PARAM<TAUT.PRD.ONLINE.JNL>
    JNL.FREQ      = R.TAUT.PARAM<TAUT.ONLINE.JNL.FREQ>
    OFS.USER      = R.TAUT.PARAM<TAUT.TGT.OFS.USER>
    J.USER.PASSWORD = R.TAUT.PARAM<TAUT.TGT.OFS.PASSWD>

    IF J.USER.PASSWORD EQ '' THEN
        J.USER.PASSWORD = "Welcome16$"
    END

    START.JNL       = R.TAUT.PARAM<TAUT.START.JNL>
    END.JNL         = R.TAUT.PARAM<TAUT.END.JNL>
    OFS.CONV.RTN    = R.TAUT.PARAM<TAUT.OFS.CONV.RTN>
    UPDATE.RESP     = R.TAUT.PARAM<TAUT.UPDATE.RESP>
*--- Open the main Source File from which this routine should produce OFS messages
    SOURCE.FILE     = R.TAUT.PARAM<TAUT.SOURCE.FILE>
    DATA.SYNC       = R.TAUT.PARAM<TAUT.DATA.SYNC>

*--- Check for special processing Parameters

    SPE.FILE.NAME    = R.TAUT.APP.DET<TAUT.FILE.NAME>
    OFS.PRE.CONV.RTN   = R.TAUT.APP.DET<TAUT.PRE.OFS.RTN>
    OFS.POST.CONV.RTN   = R.TAUT.APP.DET<TAUT.POST.OFS.RTN>
    MESSAGE.SYNTAX   = R.TAUT.APP.DET<TAUT.MESSAGE.SYNTAX>
    DELIM           = R.TAUT.APP.DET<TAUT.MSG.DELIMITER>
    POST.MESSAGE    = R.TAUT.APP.DET<TAUT.POST.MESSAGE>

    RETURN

OPEN.TAUT.LOG.FILE:
*******************

    T.LOG.LEVEL = R.TAUT.PARAM<TAUT.LOG.LEVEL>
    IF T.LOG.LEVEL NE 'NONE' THEN
        FN.TAUT.LOG  = R.TAUT.PARAM<TAUT.LOG.FILE>
        F.TAUT.LOG = ''
        OPEN FN.TAUT.LOG TO F.TAUT.LOG ELSE
            LOG.LEVEL = 1     ;* Fatal
            TAUT.TEXT = 'Cannot open the LOG file: ':FN.TAUT.LOG
            GOSUB UPDATE.TAUT.LOG
        END
    END

    IF TAUT.PARAM.READ.ERR OR R.TAUT.PARAM EQ '' THEN
        LOG.LEVEL = 1         ;* Fatal
        TAUT.TEXT = 'TAUT.PARAMETER RECORD MISSING'
        GOSUB UPDATE.TAUT.LOG
    END

    IF TAUT.APP.DET.READ.ERR OR R.TAUT.APP.DET EQ '' THEN
        LOG.LEVEL = 3         ;* Info
        TAUT.TEXT = 'TAUT.APPL.DETAILS RECORD MISSING'
        GOSUB UPDATE.TAUT.LOG
    END

    RETURN
UPDATE.TAUT.LOG:
****************
    YID = ''
    CALL ALLOCATE.UNIQUE.TIME(YID)
    IF TAUT.TEXT THEN
        IF T.LOG.LEVEL EQ 'FULL' THEN
            BEGIN CASE
            CASE LOG.LEVEL = 1
                TAUT.TEXT = "Critical Error :" :TIMEDATE():" ":TAUT.TEXT
            CASE LOG.LEVEL = 2
                TAUT.TEXT = "Warning  :" :TIMEDATE():" ":TAUT.TEXT
            CASE LOG.LEVEL = 3
                TAUT.TEXT = "Info :" :TIMEDATE():" ":TAUT.TEXT
            END CASE
            CALL F.WRITE(FN.TAUT.LOG,YID,TAUT.TEXT)
        END

        IF T.LOG.LEVEL EQ 'FATAL' AND LOG.LEVEL EQ 1 THEN
            TAUT.TEXT = "Critical Error :" :TIMEDATE():" ":TAUT.TEXT
            CALL F.WRITE(FN.TAUT.LOG,YID,TAUT.TEXT)
        END
    END
    TAUT.TEXT = ''
    RETURN

END


